; mult43.a
; from https://archive.org/details/6502-assembly-language-programming/page/n251/mode/2up
; from the book "6502 assembly language programming" by Lance A. Leventhal
;
; 8 bit x 8 bit unsigned multiply, 16 bit result
; Average cycles: 208.9
; 26 bytes


factorA   = $02
factorB   = $03
prod_low  = $04
prod_high = $05

* = $0200

mult
    lda #0
    sta prod_high
    ldx #8
shift
    asl
    rol prod_high
    asl factorB
    bcc chcnt
    clc
    adc factorA
    bcc chcnt
    inc prod_high
chcnt
    dex
    bne shift
    sta prod_low
    rts
